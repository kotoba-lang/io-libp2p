(ns kotoba.net.ipns-router-test
  (:require [clojure.test :refer [deftest is]]
            [ipns.record :as record]
            [kotoba.net.gossip :as gossip]
            [kotoba.net.ipns-router :as host]))

(def ipns-name "k51qzi5uqu5dg6lcd99r9gmb963kgugjinxxggwy7o93oagk3f2eg3qcjh7127")
(def validity (record/rfc3339-nanos
               {:year 2030 :month 1 :day 1 :hour 0 :minute 0 :second 0}))
(def validation {:verify-fn (fn [_ message signature]
                              (= (vec message) (vec signature)))
                 :now-ms 1767225600000})

(defn wire [sequence]
  (record/serialize
   (record/create {:value "/ipfs/bafkqaaa" :validity validity
                   :sequence sequence :sign-fn vec})))

(deftest standard-router-effects-flow-through-the-current-gossip-host
  (let [state (host/open "self" (gossip/empty-peer-state) ipns-name)
        started (host/start state)
        subscribed (host/peer-subscribed (:state started) "peer-b")
        published (host/local-publish (:state subscribed) (wire 1) validation)]
    (is (= [:pubsub/subscribe :fetch/serve] (mapv :op (:commands started))))
    (is (= :ipns-fetch (get-in subscribed [:commands 0 :message :kind])))
    (is (:accepted? published))
    (is (= [:persist/put :send] (mapv :op (:commands published))))
    (is (= :gossip (get-in published [:commands 1 :message :kind])))
    (is (= (get-in published [:state :router :topic])
           (get-in published [:commands 1 :message :topic])))))

(deftest stale-record-does-not-persist-or-fan-out
  (let [state (-> (host/open "self" (gossip/add-peer (gossip/empty-peer-state)
                                                       "peer-b" #{}) ipns-name)
                  (host/peer-subscribed "peer-b") :state)
        first-result (host/ingest state (wire 2) validation)
        stale (host/ingest (:state first-result) (wire 1) validation)]
    (is (= :not-better (:reason stale)))
    (is (empty? (:commands stale)))))

(deftest fetch-response-uses-host-envelope-command
  (let [state (host/open "self" (gossip/empty-peer-state) ipns-name)
        published (host/local-publish state (wire 1) validation)
        response (host/fetch-request (:state published) "peer-b")]
    (is (= :ipns-fetch-result (get-in response [:commands 0 :message :kind])))
    (is (= "peer-b" (get-in response [:commands 0 :to])))))
