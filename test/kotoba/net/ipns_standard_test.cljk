(ns kotoba.net.ipns-standard-test
  (:require [clojure.test :refer [deftest is]]
            [ed25519.core :as ed]
            [ipns.pubsub :as ipns]
            [ipns.record :as record]
            [kotoba.net.ipns-standard :as standard]
            [kotoba.net.libp2p.fetch :as fetch]
            [kotoba.net.libp2p.handshake :as handshake]
            [kotoba.net.libp2p.keys :as keys]
            [kotoba.net.libp2p.node :as node]
            [kotoba.net.libp2p.pubsub :as pubsub]
            [multiformats.core :as mf]))

(def ipns-name "k51qzi5uqu5dg6lcd99r9gmb963kgugjinxxggwy7o93oagk3f2eg3qcjh7127")

(defn- seed [n] (byte-array (map unchecked-byte (repeat 32 n))))

(defn- test-identity [n]
  (let [seed (seed n)
        public-key (ed/pubkey-from-seed seed)]
    {:identity-public-key public-key
     :sign-fn #(ed/sign seed (byte-array (map unchecked-byte %)))
     :verify-fn (keys/verifier)
     :peer-id (handshake/peer-id mf/sha256
                                 (handshake/public-key-protobuf public-key))}))

(deftest ipns-topic-is-carried-by-standard-strict-no-sign-rpc
  (let [router (ipns/init ipns-name)
        subscription (pubsub/decode-rpc (standard/subscription-rpc router true))
        publication (pubsub/decode-rpc (standard/publication-rpc router [1 2 3]))]
    (is (= [{:subscribe? true :topic (:topic router)}]
           (:subscriptions subscription)))
    (is (= [{:topic (:topic router) :data [1 2 3]}]
           (:messages publication)))))

(deftest subscription-fetch-updates-ipns-state-over-the-standard-wire
  (let [validity (record/rfc3339-nanos
                  {:year 2030 :month 1 :day 1 :hour 0 :minute 0 :second 0})
        bytes (record/serialize
               (record/create {:value "/ipfs/bafkqaaa"
                               :validity validity
                               :sequence 7
                               :sign-fn vec}))
        serving (atom (assoc (ipns/init ipns-name) :record-bytes bytes))
        receiving (atom (ipns/init ipns-name))
        server (node/node
                (assoc (test-identity 41) :protocol-handlers
                       {fetch/protocol (standard/fetch-handler serving)}))
        listener (node/listen! server {:host "127.0.0.1" :port 0})
        validation {:verify-fn (fn [_ message signature]
                                 (= (vec message) (vec signature)))
                    :now-ms 1767225600000}]
    (try
      (let [result (standard/fetch-peer!
                    receiving
                    (str "/ip4/127.0.0.1/tcp/" (:port listener))
                    (test-identity 42)
                    validation)]
        (is (:accepted? result))
        (is (= :ok (:fetch-status result)))
        (is (= 7 (get-in @receiving [:record :sequence])))
        (is (= [:persist/put :pubsub/publish] (mapv :op (:effects result)))))
      (finally ((:stop! listener))))))
