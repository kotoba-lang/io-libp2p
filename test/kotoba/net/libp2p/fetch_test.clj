(ns kotoba.net.libp2p.fetch-test
  (:require [clojure.test :refer [deftest is testing]]
            [ed25519.core :as ed]
            [kotoba.net.libp2p.fetch :as fetch]
            [kotoba.net.libp2p.handshake :as handshake]
            [kotoba.net.libp2p.keys :as keys]
            [kotoba.net.libp2p.node :as node]
            [multiformats.core :as mf]))

(defn- seed [n] (byte-array (map unchecked-byte (repeat 32 n))))

(defn- test-identity [n]
  (let [seed (seed n)
        public-key (ed/pubkey-from-seed seed)]
    {:identity-public-key public-key
     :sign-fn #(ed/sign seed (byte-array (map unchecked-byte %)))
     :verify-fn (keys/verifier)
     :peer-id (handshake/peer-id mf/sha256
                                 (handshake/public-key-protobuf public-key))}))

(deftest protobuf-wire-matches-the-fetch-contract
  (is (= [10 2 1 2] (fetch/encode-request [1 2])))
  (is (= {:identifier [1 2]} (fetch/decode-request [10 2 1 2])))
  (testing "proto3 omits the zero-valued OK enum"
    (is (= [18 3 97 98 99]
           (fetch/encode-response {:status :ok :data [97 98 99]})))
    (is (= {:status :ok :data [97 98 99]}
           (fetch/decode-response [18 3 97 98 99]))))
  (is (= [8 1] (fetch/encode-response {:status :not-found})))
  (is (= {:status :not-found} (fetch/decode-response [8 1]))))

(deftest fetch-crosses-real-tcp-noise-yamux-and-authenticated-stream
  (let [seen (promise)
        server-id (test-identity 31)
        client-id (test-identity 32)
        server (node/node
                (assoc server-id :protocol-handlers
                       {fetch/protocol
                        (fetch/handler
                         (fn [{:keys [identifier peer-id]}]
                           (deliver seen {:identifier identifier :peer-id peer-id})
                           (when (= [1 2 3] identifier) [9 8 7])))}))
        listener (node/listen! server {:host "127.0.0.1" :port 0})
        address (str "/ip4/127.0.0.1/tcp/" (:port listener))]
    (try
      (is (= {:status :ok :data [9 8 7]}
             (fetch/request! address client-id [1 2 3])))
      (is (= {:identifier [1 2 3] :peer-id (:peer-id client-id)}
             (deref seen 2000 nil)))
      (is (= {:status :not-found}
             (fetch/request! address client-id [4 5 6])))
      (finally ((:stop! listener))))))

(deftest handler-converts-failure-to-protocol-error
  (let [server (node/node
                (assoc (test-identity 33) :protocol-handlers
                       {fetch/protocol (fetch/handler (fn [_] (throw (ex-info "private" {}))))}))
        listener (node/listen! server {:host "127.0.0.1" :port 0})]
    (try
      (is (= {:status :error}
             (fetch/request! (str "/ip4/127.0.0.1/tcp/" (:port listener))
                             (test-identity 34) [1])))
      (finally ((:stop! listener))))))
