(ns kotoba.net.libp2p.dcutr-test
  (:require [clojure.test :refer [deftest is testing]]
            [kotoba.net.libp2p.dcutr :as dcutr]
            [kotoba.net.libp2p.socket :as socket]))

(def addr-a [4 127 0 0 1 6 15 161])
(def addr-b [4 127 0 0 1 145 2 206 206])

(deftest standard-protobuf-roundtrip-and-bounds
  (is (= [8 100 18 8 4 127 0 0 1 6 15 161]
         (dcutr/encode-message {:type :connect :observed-addrs [addr-a]})))
  (is (= [8 172 2] (dcutr/encode-message {:type :sync})))
  (is (= {:type :connect :observed-addrs [addr-a addr-b]}
         (dcutr/decode-message
          (dcutr/encode-message {:type :connect :observed-addrs [addr-a addr-b]}))))
  (is (= {:type :sync :observed-addrs []}
         (dcutr/decode-message (dcutr/encode-message {:type :sync}))))
  (is (thrown? clojure.lang.ExceptionInfo
               (dcutr/encode-message {:type :connect :observed-addrs []})))
  (is (thrown? clojure.lang.ExceptionInfo
               (dcutr/decode-message (vec (repeat 4097 0))))))

(deftest relay-stream-upgrades-both-sides-to-the-authenticated-peer
  (let [{:keys [a b]} (socket/pair)
        dialed-a (atom []) dialed-b (atom [])
        connection (fn [peer address]
                     {:peer-id peer :authenticated? true :address address
                      :open-stream! identity :close! (fn [])})
        responder (future
                    (dcutr/respond!
                     b [addr-b] "initiator" (constantly true)
                     (fn [address] (swap! dialed-b conj address)
                       (connection "initiator" address))))
        initiator (dcutr/initiate!
                   a [addr-a] "responder" (constantly true)
                   (fn [address] (swap! dialed-a conj address)
                     (connection "responder" address)))]
    (is (= "responder" (:peer-id initiator)))
    (is (= "initiator" (:peer-id @responder)))
    (is (= [addr-b] @dialed-a))
    (is (= [addr-a] @dialed-b))))

(deftest direct-dial-is-capped-and-peer-bound
  (let [{:keys [a b]} (socket/pair)
        remote-addrs [[1] [2] [3] [4]]
        attempts (atom 0)
        responder (future
                    (dcutr/respond!
                     b remote-addrs "initiator" (constantly true)
                     (fn [_] {:peer-id "initiator" :authenticated? true
                              :open-stream! identity :close! (fn [])})))]
    (testing "the initiator cannot increase the three-attempt protocol budget"
      (is (thrown? clojure.lang.ExceptionInfo
                   (dcutr/initiate!
                    a [[9]] "responder" (constantly true)
                    (fn [_] (swap! attempts inc) (throw (ex-info "down" {}))))))
      (is (= dcutr/max-connect-attempts @attempts)))
    @responder))
