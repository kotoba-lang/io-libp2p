(ns kotoba.net.libp2p.transport-test
  (:require [clojure.test :refer [deftest is testing]]
            [kotoba.net.libp2p.transport :as transport]))

(deftest address-kind-is-most-specific
  (is (= :quic-v1 (transport/kind "/ip4/127.0.0.1/udp/443/quic-v1")))
  (is (= :webtransport (transport/kind "/dns/example/udp/443/quic-v1/webtransport")))
  (is (= :webrtc-direct (transport/kind "/ip4/127.0.0.1/udp/4001/webrtc-direct")))
  (is (= :webrtc (transport/kind "/p2p/relay/p2p-circuit/webrtc/p2p/target")))
  (is (= :relay (transport/kind "/p2p/relay/p2p-circuit/p2p/target")))
  (is (= "target" (transport/target-peer-id
                    "/p2p/relay/p2p-circuit/p2p/target"))))

(deftest registry-falls-back-but-never-weakens-peer-authentication
  (let [closed (atom 0)
        registry (transport/registry
                  3
                  {:quic-v1 (fn [_ _] (throw (ex-info "down" {})))
                   :webtransport (fn [_ _]
                                   {:peer-id "target" :authenticated? true
                                    :open-stream! (fn [_] :stream)
                                    :close! #(swap! closed inc)})})
        connection (transport/dial!
                    registry
                    ["/ip4/127.0.0.1/udp/1/quic-v1/p2p/target"
                     "/ip4/127.0.0.1/udp/2/quic-v1/webtransport/p2p/target"])]
    (is (= :webtransport (:transport connection)))
    (is (= :stream ((:open-stream! connection) "/ipfs/graphsync/2.0.0")))
    (testing "an authenticated but different peer is still rejected"
      (is (thrown-with-msg?
           clojure.lang.ExceptionInfo #"candidate-limit"
           (transport/dial!
            (transport/registry
             1 {:quic-v1 (fn [_ _]
                           {:peer-id "attacker" :authenticated? true
                            :open-stream! identity :close! #(swap! closed inc)})})
            ["/ip4/127.0.0.1/udp/1/quic-v1/p2p/target"]))))
    (is (= 1 @closed))))

(deftest candidate-budget-is-host-owned
  (let [calls (atom 0)
        registry (transport/registry
                  1 {:quic-v1 (fn [_ _]
                                (swap! calls inc)
                                (throw (ex-info "down" {})))})]
    (is (thrown-with-msg?
         clojure.lang.ExceptionInfo #"candidate-limit"
         (transport/dial! registry
                          ["/udp/1/quic-v1" "/udp/2/quic-v1"]
                          {:max-candidates 99})))
    (is (= 1 @calls))))
