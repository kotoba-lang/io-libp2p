(ns kotoba.net.libp2p.pubsub-stream-test
  (:require [clojure.test :refer [deftest is]]
            [ed25519.core :as ed]
            [kotoba.net.libp2p.handshake :as handshake]
            [kotoba.net.libp2p.keys :as keys]
            [kotoba.net.libp2p.node :as node]
            [kotoba.net.libp2p.pubsub :as pubsub]
            [kotoba.net.libp2p.pubsub-stream :as stream]
            [multiformats.core :as mf]))

(defn- seed [n] (byte-array (map unchecked-byte (repeat 32 n))))
(defn- test-identity [n]
  (let [seed (seed n) public-key (ed/pubkey-from-seed seed)]
    {:identity-public-key public-key
     :sign-fn #(ed/sign seed (byte-array (map unchecked-byte %)))
     :verify-fn (keys/verifier)
     :peer-id (handshake/peer-id mf/sha256 (handshake/public-key-protobuf public-key))}))

(deftest multiple-rpcs-cross-one-long-lived-authenticated-gossipsub-stream
  (let [received (atom [])
        signal (promise)
        client (test-identity 51)
        server (node/node
                (assoc (test-identity 52) :protocol-handlers
                       {pubsub/gossipsub-v1-1
                        (stream/handler
                         (fn [item]
                           (swap! received conj item)
                           (when (= 3 (count @received)) (deliver signal true))))}))
        listener (node/listen! server {:host "127.0.0.1" :port 0})
        writer (stream/open-writer! (str "/ip4/127.0.0.1/tcp/" (:port listener)) client)]
    (try
      ((:write! writer) {:subscriptions [{:topic "/record/x" :subscribe? true}]})
      ((:write! writer) {:control {:graft [{:topic "/record/x"}]}})
      ((:write! writer) {:messages [{:topic "/record/x" :data [1 2 3]}]})
      (is (true? (deref signal 3000 false)))
      (is (= (:peer-id client) (:peer-id (first @received))))
      (is (= [true false false]
             (mapv #(some? (get-in % [:rpc :subscriptions 0 :subscribe?])) @received)))
      (is (= [1 2 3] (get-in @received [2 :rpc :messages 0 :data])))
      (finally
        ((:close! writer))
        ((:stop! listener))))))
